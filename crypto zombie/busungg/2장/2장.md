# 컨트렉트

### 1. 매핑과 주소 자료형

1. 매핑(mapping)
   솔리디티에서 구조화된 데이터를 저장하는 또다른 방법(구조체, 배열)입니다.

   ```
    // 금융 앱용으로, 유저의 계좌 잔액을 보유하는 uint를 저장한다:
   mapping (address => uint) public accountBalance;
   // 혹은 userID로 유저 이름을 저장/검색하는 데 매핑을 쓸 수도 있다
   mapping (uint => string) userIdToName;
   ```

   매핑은 기본적으로 key-value 저장소로, 데이터를 저장하고 검색하는데 이용됩니다.

2. 어드레스(address)
   이더리움 블록체인은 은행 계좌와 같은 계정들로 이루어져 있습니다. 계정은 이더리움 블록체인상의 통화인 _이더_ 의 잔액을 가지는데  
   계정을 통해 다른 계정과 이더를 주고 받을 수 있습니다.  
   각 계정은 은행 계좌 번호와 같은 주소를 가지고 있으며 주소는 특정 계정을 가리키는 고유 식별자입니다.
   > 즉 주소는 특정 유저(혹은 스마트 컨트랙트)가 소유합니다.

### 2. Msg.sender

솔리디티에는 모든 함수에서 이용 가능한 특정 전역 변수들이 있습니다. 그 중의 하나가 현재 함수를 호출한 사람 (혹은 스마트 컨트랙트)의 주소를 가리키는 msg.sender입니다.

참고: 솔리디티에서 함수 실행은 항상 외부 호출자가 시작합니다.  
컨트랙트는 누군가가 컨트랙트의 함수를 호출할 때까지 블록체인 상에서 아무 것도 안 하고 있을 것입니다. 그러니 항상 msg.sender가 있어야 합니다.

```
mapping (address => uint) favoriteNumber;

function setMyNumber(uint _myNumber) public {
  // `msg.sender`에 대해 `_myNumber`가 저장되도록 `favoriteNumber` 매핑을 업데이트한다 `
  favoriteNumber[msg.sender] = _myNumber;
  // ^ 데이터를 저장하는 구문은 배열로 데이터를 저장할 떄와 동일하다
}

function whatIsMyNumber() public view returns (uint) {
  // sender의 주소에 저장된 값을 불러온다
  // sender가 `setMyNumber`을 아직 호출하지 않았다면 반환값은 `0`이 될 것이다
  return favoriteNumber[msg.sender];
}
```

### 3. Require

require를 활용하면 특정 조건이 참이 아닐 때 함수가 에러 메시지를 발생하고 실행을 멈추게 됩니다.

```
function sayHiToVitalik(string _name) public returns (string) {
  // _name이 "Vitalik"인지 비교한다. 참이 아닐 경우 에러 메시지를 발생하고 함수를 벗어난다
  // (참고: 솔리디티는 고유의 스트링 비교 기능을 가지고 있지 않기 때문에
  // 스트링의 keccak256 해시값을 비교하여 스트링 값이 같은지 판단한다)
  require(keccak256(_name) == keccak256("Vitalik"));
  // 참이면 함수 실행을 진행한다:
  return "Hi!";
}

//sayHiToVitalik("Vitalik")로 이 함수를 실행하면 "Hi!"가 반환될 것이네.
//"Vitalik"이 아닌 다른 값으로 이 함수를 호출할 경우, 에러 메시지가 뜨고 함수가 실행되지 않을 걸세.
```

### 4. 상속

엄청나게 긴 컨트랙트 하나를 만들기 보다는 코드를 잘 정리해서 여러 컨트랙트에 코드 로직을 나누는 것이 합리적일 때가 있습니다.

이를 보다 관리하기 쉽도록 하는 솔리디티 기능이 바로 컨트랙트 _상속_ 입니다.

```
contract Doge {
  function catchphrase() public returns (string) {
    return "So Wow CryptoDoge";
  }
}

contract BabyDoge is Doge {
  function anotherCatchphrase() public returns (string) {
    return "Such Moon BabyDoge";
  }
}
```

BabyDoge 컨트랙트는 Doge 컨트랙트를 상속합니다.  
즉, BabyDoge 컨트랙트를 컴파일해서 구축할 때, BabyDoge 컨트랙트가 catchphrase() 함수와 anotherCatchphrase() 함수에 모두 접근할 수 있다는 뜻입니다.  
(Doge 컨트랙트에 정의되는 다른 어떤 public 함수가 정의되어도 접근이 가능합니다.)

### 6. Import

코드가 꽤 길어지고 있으니, 여러 파일로 나누어 정리하면 관리하기 더 편하게 됩니다.  
보통 이런 방식으로 솔리디티 프로젝트의 긴 코드를 처리할 것인데요

다수의 파일이 있고 어떤 파일을 다른 파일로 불러오고 싶을 때, 솔리디티는 import라는 키워드를 이용합니다.

```
import "./someothercontract.sol";

contract newContract is SomeOtherContract {

}
```

### 7. Storage, Memory

솔리디티에는 변수를 저장할 수 있는 공간으로 storage와 memory 두 가지가 있습니다.

1. Storage

   - 블록체인 상에 영구적으로 저장되는 변수를 의미합니다.

2. Memory
   - 임시적으로 저장되는 변수로, 컨트랙트 함수에 대한 외부 호출들이 일어나는 사이에 지워집니다.

대부분의 경우에 이런 키워드를 이용할 필요가 없는데요 솔리디티가 알아서 처리해 주기 때문입니다.  
상태 변수(함수 외부에 선언된 변수)는 초기 설정상 storage로 선언되어 블록체인에 영구적으로 저장되는 반면, 함수 내에 선언된 변수는 memeory로 자동 선언되어서 함수 호출이 종료되면 사라집니다.

하지만 이 키워드들을 사용해야 때가 있는데 바로 함수 내의 구조체와 _배열_ 을 처리할 때입니다.

```
contract SandwichFactory {
  struct Sandwich {
    string name;
    string status;
  }

  Sandwich[] sandwiches;

  function eatSandwich(uint _index) public {
    // Sandwich mySandwich = sandwiches[_index];

    // ^ 꽤 간단해 보이나, 솔리디티는 여기서
    // `storage`나 `memory`를 명시적으로 선언해야 한다는 경고 메시지를 발생한다.
    // 그러므로 `storage` 키워드를 활용하여 다음과 같이 선언해야 한다:
    Sandwich storage mySandwich = sandwiches[_index];
    // ...이 경우, `mySandwich`는 저장된 `sandwiches[_index]`를 가리키는 포인터이다.
    // 그리고
    mySandwich.status = "Eaten!";
    // ...이 코드는 블록체인 상에서 `sandwiches[_index]`을 영구적으로 변경한다.

    // 단순히 복사를 하고자 한다면 `memory`를 이용하면 된다:
    Sandwich memory anotherSandwich = sandwiches[_index + 1];
    // ...이 경우, `anotherSandwich`는 단순히 메모리에 데이터를 복사하는 것이 된다.
    // 그리고
    anotherSandwich.status = "Eaten!";
    // ...이 코드는 임시 변수인 `anotherSandwich`를 변경하는 것으로
    // `sandwiches[_index + 1]`에는 아무런 영향을 끼치지 않는다. 그러나 다음과 같이 코드를 작성할 수 있다:
    sandwiches[_index + 1] = anotherSandwich;
    // ...이는 임시 변경한 내용을 블록체인 저장소에 저장하고자 하는 경우이다.
  }
}
```

### 8. 함수 접근 제어자 더 알아보기

1. internal
   - 함수가 정의된 컨트랙트를 상속하는 컨트랙트에서도 접근이 가능합니다.
   - 이 이외에는 private과 동일하게 외부 컨트랙트에서 접근이 불가합니다.
2. external

   - 함수가 컨트랙트 바깥에서만 호출될 수 있고 컨트랙트 내의 다름 함수에 의해 호출될 수 없습니다.
   - 이 이외에는 public과 동일합니다.

3. 예시

   - ```
      contract Sandwich {
        uint private sandwichesEaten = 0;

        function eat() internal {
          sandwichesEaten++;
        }
      }

      contract BLT is Sandwich {
        uint private baconSandwichesEaten = 0;

        function eatWithBacon() public returns (string) {
          baconSandwichesEaten++;
          // eat 함수가 internal로 선언되었기 때문에 여기서 호출이 가능하다
          eat();
        }
      }
     ```

### 9. 다른 컨트랙트와 상호작용하기

블록체인 상에 있으면서 우리가 소유하지 않은 컨트랙트와 우리 컨트랙트가 상호작용을 하려면 우선 인터페이스를 정의해야 합니다.

```
contract LuckyNumber {
  mapping(address => uint) numbers;

  function setNum(uint _num) public {
    numbers[msg.sender] = _num;
  }

  function getNum(address _myAddress) public view returns (uint) {
    return numbers[_myAddress];
  }
}

contract NumberInterface {
  //인터페이스

  function getNum(address _myAddress) public view returns (uint);
}
```

인터페이스 정의는 컨트랙트를 정의하는 것과 유사합니다.  
다른 컨트랙트와 상호작용하고자 하는 함수만을 선언할 뿐 다른 함수나 상태 변수를 언급하지 않습니다.  
다음으로, 함수 몸체를 정의하지 않습니다. 중괄호 {, }를 쓰지 않고 함수 선언을 세미콜론(;)으로 간단하게 끝냅니다.  
그러니 인터페이스는 컨트랙트 뼈대처럼 보인다고 할 수 있으며 컴파일러도 그렇게 인터페이스를 인식합니다.

### 10. 인터페이스 활용하기

아래와 같이 인터페이스가 선언되어있다면

```
contract NumberInterface {
  function getNum(address _myAddress) public view returns (uint);
}
```

다음과 같이 컨트랙트에서 인터페이스를 사용할 수 있습니다.

```
contract MyContract {
  address NumberInterfaceAddress = 0xab38...
  // ^ 이더리움상의 FavoriteNumber 컨트랙트 주소이다
  NumberInterface numberContract = NumberInterface(NumberInterfaceAddress)
  // 이제 `numberContract`는 다른 컨트랙트를 가리키고 있다.

  function someFunction() public {
    // 이제 `numberContract`가 가리키고 있는 컨트랙트에서 `getNum` 함수를 호출할 수 있다:
    uint num = numberContract.getNum(msg.sender);
    // ...그리고 여기서 `num`으로 무언가를 할 수 있다
    //물론 상호작용하는 함수가 public이나 external로 선언되어 있어야 합니다.
  }
}
```

### 11. 다수의 반환값 처리하기

```
function multipleReturns() internal returns(uint a, uint b, uint c) {
  return (1, 2, 3);
}

function processMultipleReturns() external {
  uint a;
  uint b;
  uint c;
  // 다음과 같이 다수 값을 할당한다:
  (a, b, c) = multipleReturns();
}

// 혹은 단 하나의 값에만 관심이 있을 경우:
function getLastReturnValue() external {
  uint c;
  // 다른 필드는 빈칸으로 놓기만 하면 된다:
  (,,c) = multipleReturns();
}
```

### 12. IF 문

If 문
솔리디티에서 if 문은 자바스크립트의 if 문과 동일하다:

```
function eatBLT(string sandwich) public {
  // 스트링 간의 동일 여부를 판단하기 위해 keccak256 해시 함수를 이용해야 한다는 것을 기억하자
  if (keccak256(sandwich) == keccak256("BLT")) {
    eat();
  }
}
```
